import dev.polariscore.tasks.Utils
import dev.polariscore.tasks.CreateTextFile

plugins {
    id("java")
}

group = "dev.polariscore"
version = "1.21-" + (System.getenv("COMMIT_COUNT") == null ? "dev" : System.getenv("COMMIT_COUNT"))
base.archivesName.set("PolarisCore")

repositories {
    mavenCentral()
}

dependencies {
    implementation("net.minestom:minestom-snapshots:7ce047b22e")
    implementation("ch.qos.logback:logback-classic:1.5.7")
}

tasks.register("createLibrariesFile", CreateTextFile) {
    fileName = "libraries.txt"
    content = Utils.getDependencies(configurations.implementation.copy())
}

tasks.register("createRepositoriesFile", CreateTextFile) {
    fileName = "repositories.txt"
    content = Utils.getRepositories(repositories)
}

tasks.jar {
    from(createLibrariesFile)
    from(createRepositoriesFile)

    manifest {
        attributes(
                "Launcher-Agent-Class": "dev.polariscore.server.launcher.Agent",
                "Agent-Class": "dev.polariscore.server.launcher.Agent",
                "Premain-Class": "dev.polariscore.server.launcher.Agent",
                "Main-Class": "dev.polariscore.server.launcher.Launcher",
                "Multi-Release": true
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}


